TITLE: example x: logistic regression with fiml estimation;
DATA: 
file = ../data/behaviorachievement.dat;
VARIABLE: 
names = id male hispanic atriskgrp atrisk behsymp1 lrnprob1 
   read1 read2 read3 read9 readgrp9 stanread7 
   math1 math2 math3 math9 mathgrp9 stanmath7;
usevariables = readgrp9 read1 lrnprob1 behsymp1;
categorical = readgrp9;
missing = all(999);
ANALYSIS:
! set estimator to maximum likelihood;
! set link function as logit;
! set integration method to montecarlo;
estimator = mlr;
link = logit;
integration = montecarlo;
MODEL:  
! normal distribution for predictors;
read1 lrnprob1 behsymp1;
! focal logistic model;
readgrp9 on read1 lrnprob1 behsymp1 (beta1-beta3);
MODEL TEST:
! wald test that each slope equals 0;
0 = beta1; 0 = beta2; 0 = beta3;
OUTPUT:
! output for missing data patterns, sample statistics, standardized coefficients, and confidence intervals;
sampstat patterns cinterval;
