TITLE: fiml logistic regression with auxiliary variables;
DATA: 
file = behaviorachievement.dat;
VARIABLE: 
names = id male hispanic riskgrp atrisk behsymp1 lrnprob1 
   read1 read2 read3 read9 readgrp9 stanread7 
   math1 math2 math3 math9 mathgrp9 stanmath7;
usevariables = hispanic readgrp9 read1 lrnprob1 behsymp1 read2 stanread7;
categorical = hispanic readgrp9;
missing = all(999);
ANALYSIS:
! set estimator to maximum likelihood;
estimator = ml;
! set link function as logit;
link = logit;
! set integration method to montecarlo;
integration = montecarlo;
MODEL:  
! normal distribution for predictors;
read1 lrnprob1 behsymp1;
! focal logistic model;
readgrp9 on read1 lrnprob1 behsymp1 (beta1-beta3);
! model for read9grp outcome;
hispanic on readgrp9 read1 lrnprob1 behsymp1;
! model for read2 auxiliary variable;
read2 on hispanic readgrp9 read1 lrnprob1 behsymp1;
! model for stanread7 auxiliary variable;
stanread7 on read2 hispanic readgrp9 read1 lrnprob1 behsymp1;
MODEL TEST:
! wald test that each slope equals 0;
0 = beta1; 0 = beta2; 0 = beta3;
OUTPUT:
! output for missing data patterns, sample statistics, standardized coefficients, and confidence intervals;
patterns sampstat stdyx cinterval;

